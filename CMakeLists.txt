#
# Apokalypse3D - Fast and cache-friendly 3D game engine
# Copyright (C) 2022-2023 Yuriy Zinchenko
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.
#

CMAKE_MINIMUM_REQUIRED (VERSION 3.2 FATAL_ERROR)

IF (COMMAND CMAKE_POLICY)
	CMAKE_POLICY (SET CMP0048 NEW)
	CMAKE_POLICY (SET CMP0079 NEW)
ENDIF ()

PROJECT (Apokalypse3D VERSION 0.0.0.0)

SET (CMAKE_CXX_STANDARD 20)
SET (CMAKE_CXX_STANDARD_REQUIRED ON)
SET (CMAKE_CXX_EXTENSIONS OFF)

SET (CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib)
SET (CMAKE_LIBRARY_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib)
SET (CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin)

SET (CMAKE_DEBUG_POSTFIX _d)
SET (CMAKE_RELWITHDEBINFO_POSTFIX _rd)

SET (CMAKE_MODULE_PATH ${CMAKE_SOURCE_DIR}/CMake)

INCLUDE (Apokalypse3D REQUIRED)
INCLUDE (GenerateExportHeader REQUIRED)
INCLUDE (CompilerUtility REQUIRED)

SET (INITIAL_ASSERT OFF)
SET (INITIAL_LOG_DEBUG OFF)
SET (INITIAL_LOG_FILELINE OFF)
SET (INITIAL_LOG_LEVEL "WARNING")
IF (CMAKE_BUILD_TYPE STREQUAL Debug)
	SET (INITIAL_ASSERT ON)
	SET (INITIAL_LOG_DEBUG ON)
	SET (INITIAL_LOG_FILELINE ON)
	SET (INITIAL_LOG_LEVEL "DEBUG")
ELSEIF (EMSCRIPTEN)
	SET (INITIAL_LOG_LEVEL "FATAL")
ENDIF ()

SET (APOKALYPSE_LIB_TYPE "SHARED" CACHE STRING "Engine library type")
SET (APOKALYPSE_LOG_LEVEL "${INITIAL_LOG_LEVEL}" CACHE STRING "Default engine logging level")

SET_PROPERTY (CACHE APOKALYPSE_LIB_TYPE PROPERTY STRINGS "SHARED" "STATIC")
SET_PROPERTY (CACHE APOKALYPSE_LOG_LEVEL PROPERTY STRINGS "TRACE" "DEBUG" "WARNING" "ERROR" "FATAL" "NONE")

OPTION (APOKALYPSE_ASSERTIONS "Enable assertions (high performance impact)" ${INITIAL_ASSERT})
OPTION (APOKALYPSE_LOG_DEBUG "Enable trace debug level ability (medium performance impact)" ${INITIAL_LOG_DEBUG})
OPTION (APOKALYPSE_LOG_FILELINE "Enable file and line output in log messages (low performance impact)" ${INITIAL_LOG_FILELINE})
OPTION (APOKALYPSE_LOG_TRACE "Enable trace log level ability (huge performance impact)" OFF)
OPTION (APOKALYPSE_PACKAGING "Enable packaging resource files" ON)
OPTION (CMAKE_EXPORT_COMPILE_COMMANDS "Export compile_commands.json" OFF)
MARK_AS_ADVANCED (APOKALYPSE_ASSERTIONS)
MARK_AS_ADVANCED (APOKALYPSE_LIB_TYPE)
MARK_AS_ADVANCED (APOKALYPSE_LOG_DEBUG)
MARK_AS_ADVANCED (APOKALYPSE_LOG_FILELINE)
MARK_AS_ADVANCED (APOKALYPSE_LOG_LEVEL)
MARK_AS_ADVANCED (APOKALYPSE_LOG_TRACE)
MARK_AS_ADVANCED (APOKALYPSE_PACKAGING)
MARK_AS_ADVANCED (CMAKE_EXPORT_COMPILE_COMMANDS)

IF (NOT EMSCRIPTEN)
	SET (APOKALYPSE_VECTORISATION "SSE4" CACHE STRING "Engine vectorisation level")
	SET_PROPERTY (CACHE APOKALYPSE_VECTORISATION PROPERTY STRINGS "NONE" "SSE3" "SSE4" "AVX" "AVX2" "AVX512")
	SET_ARCHITECTURE (${APOKALYPSE_VECTORISATION})
ENDIF ()

SET_CXX_EXCEPTIONS (OFF)
SET_CXX_RTTI (OFF)

IF (CMAKE_EXPORT_COMPILE_COMMANDS AND NOT "${CMAKE_SOURCE_DIR}" STREQUAL "${CMAKE_BINARY_DIR}")
	EXECUTE_PROCESS (COMMAND
		${CMAKE_COMMAND} -E create_symlink
		${CMAKE_CURRENT_BINARY_DIR}/compile_commands.json
		${CMAKE_SOURCE_DIR}/compile_commands.json
	)
ENDIF ()

IF (CMAKE_EXTRA_GENERATOR)
	IF ("${CMAKE_EXTRA_GENERATOR}" STREQUAL "CodeBlocks")
		SET (CMAKE_CODEBLOCKS_EXCLUDE_EXTERNAL_FILES ON)
	ELSEIF ("${CMAKE_EXTRA_GENERATOR}" STREQUAL "CodeLite")
		SET (CMAKE_CODELITE_USE_TARGETS ON)
	ENDIF ()
ENDIF ()

ADD_SUBDIRECTORY (Source)
