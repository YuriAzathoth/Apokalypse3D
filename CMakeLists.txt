#
# Apokalypse3D - Fast and cache-friendly 3D game engine
# Copyright (C) 2022-2023 Yuriy Zinchenko
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.
#

CMAKE_MINIMUM_REQUIRED (VERSION 3.2 FATAL_ERROR)

IF (COMMAND CMAKE_POLICY)
	CMAKE_POLICY (SET CMP0048 NEW)
	CMAKE_POLICY (SET CMP0079 NEW)
ENDIF ()

PROJECT (Apokalypse3D VERSION 0.0.0.0)

SET (CMAKE_CXX_STANDARD 20)
SET (CMAKE_CXX_STANDARD_REQUIRED ON)
SET (CMAKE_CXX_EXTENSIONS OFF)

SET (CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib)
SET (CMAKE_LIBRARY_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib)
SET (CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin)

SET (CMAKE_DEBUG_POSTFIX _d)
SET (CMAKE_RELWITHDEBINFO_POSTFIX _rd)

SET (CMAKE_MODULE_PATH ${CMAKE_SOURCE_DIR}/CMake)

SET (CMAKE_EXPORT_COMPILE_COMMANDS ON)
IF (CMAKE_EXTRA_GENERATOR)
	IF ("${CMAKE_EXTRA_GENERATOR}" STREQUAL "CodeBlocks")
		SET (CMAKE_CODEBLOCKS_EXCLUDE_EXTERNAL_FILES ON)
	ELSEIF ("${CMAKE_EXTRA_GENERATOR}" STREQUAL "CodeLite")
		SET (CMAKE_CODELITE_USE_TARGETS ON)
	ENDIF ()
	IF (NOT "${CMAKE_SOURCE_DIR}" STREQUAL "${CMAKE_BINARY_DIR}")
		EXECUTE_PROCESS (COMMAND
			${CMAKE_COMMAND} -E create_symlink
			${CMAKE_CURRENT_BINARY_DIR}/compile_commands.json
			${CMAKE_SOURCE_DIR}/compile_commands.json
		)
	ENDIF ()
ENDIF ()

INCLUDE (Apokalypse3D REQUIRED)
INCLUDE (GenerateExportHeader REQUIRED)

SET (APOKALYPSE_LIB_TYPE "SHARED" CACHE STRING "Engine library type")
SET_PROPERTY (CACHE APOKALYPSE_LIB_TYPE PROPERTY STRINGS "SHARED" "STATIC")
MARK_AS_ADVANCED (APOKALYPSE_LIB_TYPE)

SELECT_COMPILE_ARCHITECTURE ()

ADD_SUBDIRECTORY (ThirdParty)
ADD_SUBDIRECTORY (Tools)
ADD_SUBDIRECTORY (Engine)

IF (CMAKE_PROJECT_NAME STREQUAL PROJECT_NAME)
	OPTION (APOKALYPSE_SAMPLES "Enable building samples" ON)
	IF (APOKALYPSE_SAMPLES)
		ADD_SUBDIRECTORY (Samples)
	ENDIF ()

	IF (NOT EMSCRIPTEN)
		OPTION (APOKALYPSE_BENCHMARKS "Enable building benchmarks" ON)
		OPTION (APOKALYPSE_TESTS "Enable building tests" ON)
		MARK_AS_ADVANCED (APOKALYPSE_BENCHMARKS)
		MARK_AS_ADVANCED (APOKALYPSE_SAMPLES)
		MARK_AS_ADVANCED (APOKALYPSE_TESTS)
		IF (APOKALYPSE_BENCHMARKS)
			ADD_SUBDIRECTORY (Benchmarks)
		ENDIF ()
		IF (APOKALYPSE_TESTS)
			INCLUDE (CTest REQUIRED)
			ENABLE_TESTING ()
			ADD_SUBDIRECTORY (Tests)
		ENDIF ()
	ENDIF ()

	SET (INITIAL_ASSERT OFF)
	SET (INITIAL_LOG_DEBUG OFF)
	SET (INITIAL_LOG_FILELINE OFF)
	SET (INITIAL_LOG_LEVEL "WARNING")
	IF (CMAKE_BUILD_TYPE STREQUAL Debug)
		SET (INITIAL_ASSERT ON)
		SET (INITIAL_LOG_DEBUG ON)
		SET (INITIAL_LOG_FILELINE ON)
		SET (INITIAL_LOG_LEVEL "DEBUG")
	ELSEIF (EMSCRIPTEN)
		SET (INITIAL_LOG_LEVEL "FATAL")
	ENDIF ()
	SET (APOKALYPSE_LOG_LEVEL "${INITIAL_LOG_LEVEL}" CACHE STRING "Default engine logging level")
	SET_PROPERTY (CACHE APOKALYPSE_LOG_LEVEL PROPERTY STRINGS "TRACE" "DEBUG" "WARNING" "ERROR" "FATAL" "NONE")
	MARK_AS_ADVANCED (APOKALYPSE_LOG_LEVEL)
	
	OPTION (APOKALYPSE_ASSERTIONS "Enable assertions (high performance impact)" ${INITIAL_ASSERT})
	OPTION (APOKALYPSE_LOG_DEBUG "Enable trace debug level ability (medium performance impact)" ${INITIAL_LOG_DEBUG})
	OPTION (APOKALYPSE_LOG_FILELINE "Enable file and line output in log messages (low performance impact)" ${INITIAL_LOG_FILELINE})
	OPTION (APOKALYPSE_LOG_TRACE "Enable trace log level ability (huge performance impact)" OFF)
	MARK_AS_ADVANCED (APOKALYPSE_ASSERTIONS)
	MARK_AS_ADVANCED (APOKALYPSE_LOG_DEBUG)
	MARK_AS_ADVANCED (APOKALYPSE_LOG_FILELINE)
	MARK_AS_ADVANCED (APOKALYPSE_LOG_TRACE)
ENDIF ()
